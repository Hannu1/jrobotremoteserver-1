| *** Settings *** |
| Test Template  | Return Value Should Be |
| Resource       | resource.txt |

| *** Test Cases *** |
| Return String |
|    | Return String | Hello, world! |

| Return Empty String |
|    | Return Empty String | ${EMPTY} |

| Return Unicode String |
|    | Return Unicode String | ${UNICODE STRING} |

| Return Control Char |
|    | [Documentation] | Fails with ExpatError when running tests on Jython 2.5. |
|    | Return Control Char | ${CONTROL CHAR} |

| Return Integer |
|    | Return Integer | ${42} |
|    | Return Negative Integer | ${-1} |

| Return Float |
|    | Return Float | ${3.14} |
|    | Return Negative Float | ${-0.5} |

| Return Zero |
|    | Return Zero | ${0} |

| Return Boolean |
|    | Return Boolean True | ${True} |
|    | Return Boolean False | ${False} |

| Return None |
|    | [Documentation] | None is not supported by all XML-RPC versions and thus it is converted to an empty string |
|    | Return Nothing | ${EMPTY} |

| Return Arbitrary Object |
|    | [Documentation] | Arbitrary objects cannot be transferred over XML-RPC and thus only their string presentation is returned |
|    | Return Object | <MyObject> |

| Return List |
|    | Return List | ${LIST} |
|    | Return Empty List | ${EMPTY LIST} |

| Return List Containing None |
|    | Return List Containing None | [''] | eval |

| Return List Containing Arbitrary Objects |
|    | Return List Containing Objects | ['<MyObject1>', '<MyObject2>'] | eval |

| Return Nested List |
|    | Return Nested List | [[True, False], [[1, '', '<MyObject>', {}]]] | eval |

| Return Dictionary |
|    | Return Dictionary | ${DICT} |
|    | Return Empty Dictionary | ${EMPTY DICT} |

| Return Dictionary With Non-String Keys |
|    | [Documentation] | XML-RPC supports only strings as keys so must convert them |
|    | Return Dictionary With Non String Keys | {'1': 2, '': 1} | eval |

| Return Dictionary Containing None |
|    | Return Dictionary Containing None | {'As value': '', '': 'As key'} | eval |

| Return Dictionary Containing Objects |
|    | Return Dictionary Containing Objects | {'As value': '<MyObject1>', '<MyObject2>': 'As key'} | eval |

| Return Nested Dictionary |
|    | Return Nested Dictionary | {'1': {'': False}, '2': {'A': {'n': ''}, 'B': {'o': '<MyObject>', 'e': {}}}} | eval |

| Return Array Containing Strings |
|    | Return Array Containing Strings | ['spam', 'eggs'] | eval |

| Return Array Containing Integers |
|    | Return Array Of Short | [${34}, ${-98}] | eval |
|    | Return Primitive Array Of Short | [${287}, ${-86}] | eval |
|    | Return Array Of Integer | [${3}, ${-5}] | eval |
|    | Return Primitive Array Of Int | [${-338}, ${897}] | eval |
|    | Return Array Of Long | ['-9223372036854775800', '9223372036854775805'] | eval | # strings as longs do not fit in I4 |
|    | Return Primitive Array Of Long | ['8723372036854774829', '-9123372036854775382'] | eval |

| Return Array Containing Rational Numbers |
|    | Return Array Of Float | [${8.2}, ${-7.38}] | eval |
|    | Return Primitive Array Of Float | [${-7.774}, ${562.1}] | eval |
|    | Return Array Of Double | [${101.6}, ${45.67}] | eval |
|    | Return Primitive Array Of Double | [${-37.11}, ${90.4}] | eval |
|    | Return Primitive Array Of Boolean | [${True}, ${False}] | eval |

| Return Array Containing Booleans |
|    | Return Array Of Boolean | [${False}, ${True}] | eval |
|    | Return Primitive Array Of Boolean | [${True}, ${False}] | eval |

| Return Array Containing Chars |
|    | Return Array Containing Chars | OK |

| *** Keywords *** |
| Return Value Should Be |
|    | [Arguments] | ${keyword} | ${expected} | ${evaluate}= |
|    | ${actual} = | Run Keyword | ${keyword} |
|    | ${tester} = | Set Variable If | "${evaluate}" | Should Be Equal Evaluated | Should Be Equal |
|    | Run Keyword | ${tester} | ${actual} | ${expected} |

| Should Be Equal Evaluated |
|    | [Arguments] | ${actual} | ${expected} |
|    | ${expected} = | Evaluate | ${expected} |
|    | Should Be Equal | ${actual} | ${expected} |

